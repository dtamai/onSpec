package com.dtamai.onspec;

/**
 * Monitor that manages running specs.
 * @listens NewSuite Runs a new spec suite
 */
monitor SuiteRunner {
  /** @private */
  sequence<integer> specs := [];
  /** @private */
  sequence<string> results := [];
  /** @private */
  sequence<string> failures := [];
  /** @private */
  sequence<string> skips := [];

  import "TimeFormatPlugin" as time;
  /** @private */
  float beginTime;

  /** @private */
  action onload() {
    on all NewSuite() {
      beginTime := time.getMicroTime();
      spawn handleNewSuite;
    }
  }

  /** @private */
  action handleNewSuite() {
    route _SpecDiscover();

    _SpecPresent spec;
    listener disc := on all _SpecPresent():spec {
      specs.append(spec.id);
    }

    on completed _SpecDiscover() {
      disc.quit();
      runSpec();
    }

    _Failure f;
    on all _Failure():f {
      results.append("F");
      failures.append("'" + expandContextString(f.context) + "' failed: " + f.reason);
    }

    on all _Success() {
      results.append(".");
    }

    _Skip s;
    on all _Skip():s {
      results.append("S");
      skips.append("'" + expandContextString(s.context) + "' skipped");
    }

  }

  /** @private */
  action runSpec() {
    if specs.size() <= 0 then { endSuite(); }

    integer currId := specs[0];
    route _SpecBegin(currId);
    on _SpecEnd(currId) {
      specs.remove(0);
      runSpec();
    }
  }

  /** @private */
  action expandContextString(sequence<string> context) returns string {
    return " ".join(context);
  }

  /** @private */
  action endSuite() {
    print "";
    print "".join(results);
    print results.size().toString() + " example(s) in " +
      (time.getMicroTime() - beginTime).formatFixed(6) + "s | " +
      failures.size().toString() + " failure(s) | " +
      skips.size().toString() + " skip(s)";
    print "";
    if skips.size() > 0 then {
      print "Skips: ";
      print "\n".join(skips);
      print "";
    }
    if failures.size() > 0 then {
      print "Failures: ";
      print "\n".join(failures);
      print "";
    }
    die;
  }
}


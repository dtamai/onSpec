package org.onspec.internal;

using org.onspec.NewSuite;
using org.onspec.SuiteConfiguration;
using org.onspec.SuiteResult;
using org.onspec.SpecNode;
using org.onspec.SpecResult;

/**
 * @private
 * Monitor that manages running specs.
 * @listens NewSuite Runs a new spec suite
 * @listens SuiteConfiguration Configure (optional) how the suite runs
 */
monitor SuiteRunner {
  sequence<integer> specs := [];
  dictionary<integer /*id*/, ContextDescription> contexts := {};
  dictionary<integer /*id*/, ExampleDescription> examples := {};
  SuiteResult suiteResult;
  boolean isConfigured := false;
  SuiteConfiguration config;

  import "TimeFormatPlugin" as time;
  float beginTime;

  action onload() {
    on all SuiteConfiguration():config {
      isConfigured := true;
      handleConfiguration();
    }

    on all NewSuite() {
      if not isConfigured then {
        useDefaultConfiguration();
        // Do not cache configuration
        on completed SuiteConfiguration() {
          isConfigured := false;
        }
      }
      beginTime := time.getMicroTime();
      spawn handleNewSuite();
    }
  }

  action handleNewSuite() {
    suiteResult := new SuiteResult;
    suiteResult.nodes[0] := newContextNode(0, "");

    route SpecDiscover();

    SpecPresent spec;
    listener disc := on all SpecPresent():spec {
      specs.append(spec.id);
    }

    ContextDescription ctx;
    on all ContextDescription():ctx {
      contexts[ctx.id] := ctx;

      registerContext(ctx);
    }

    ExampleDescription ex;
    on all ExampleDescription():ex {
      examples[ex.id] := ex;

      registerExample(ex);
    }

    SkippedExampleDescription skip;
    on all SkippedExampleDescription():skip {
      registerSkippedExample(skip);
    }

    on completed SpecDiscover() {
      disc.quit();

      // Remove configuration items not required to run examples
      SuiteConfiguration downstreamConfig := config.clone();
      downstreamConfig.removeParam(SuiteConfiguration.randomSeedParamName);
      route SpecPrepare(downstreamConfig);

      SpecReady r;
      sequence<integer> knownSpecs := specs.clone();
      listener ready := on all SpecReady():r {
        knownSpecs.remove(knownSpecs.indexOf(r.id));
        if knownSpecs.size() = 0 then {
          ready.quit();
          runExamples();
        }
      }
    }

    Failure f;
    on all Failure():f {
      suiteResult.registerFailure(f.id,
          "'" + expandContextString(f.id) + "' failed: " + f.reason);
    }

    Success su;
    on all Success():su {
      suiteResult.registerSuccess(su.id);
    }

    Skip s;
    on all Skip():s {
      suiteResult.registerSkip(s.id,
          "'" + expandContextString(s.id) + "' skipped");
    }

    Error e;
    on all Error():e {
      suiteResult.registerError(e.id,
          "'" + expandContextString(e.id) + "' ERROR: " + e.reason);
    }

  }

  action registerContext(ContextDescription ctx) {
    suiteResult.addNode(newContextNode(ctx.id, ctx.text), ctx.getParentId());
  }

  action registerExample(ExampleDescription ex) {
    suiteResult.addNode(newExampleNode(ex.id, ex.text, ex.parentContextsId), ex.getParentId());
  }

  action registerSkippedExample(SkippedExampleDescription skip) {
    suiteResult.nodes[skip.id] := newExampleNode(skip.id, skip.text, skip.parentContextsId);
    suiteResult.registerSkip(skip.id,
        "'" + " ".join(contextsToTexts(skip.parentContextsId)) + " " + skip.text + "' " + skip.reason);
  }

  action newExampleNode(integer id, string text, sequence<integer> ancestorsId) returns SpecNode {
    SpecNode node := new SpecNode;
    node.id := id;
    node.text := text;
    node.isTerminal := true;
    node.ancestorsId := ancestorsId;
    return node;
  }

  action newContextNode(integer id, string text) returns SpecNode {
    SpecNode node := new SpecNode;
    node.id := id;
    node.text := text;
    return node;
  }

  action handleConfiguration() {
    config := config.merge(config.fromString(config.configString));
    if config.shouldRandomize() then {
      if not config.hasRandomSeed() then {
        config.setRandomSeed((new RandomGenerator).next());
        print "Random seed (use the config below to repeat examples order):";
        print "," + SuiteConfiguration.randomSeedParamName + ":" + config.getRandomSeed().toString();
      } else {
        print "Using configured random seed: " + config.getRandomSeed().toString();
      }
    } else {
      print "!! [WARN] Runing specs in order !!";
    }
  }

  action useDefaultConfiguration() {
    route new SuiteConfiguration;
  }

  // Used while running examples, is a modifiable copy of the examples list
  sequence<integer> pendingExamplesId;
  action runExamples() {
    pendingExamplesId := defineExamplesOrder(examples.keys());
    suiteResult.defineExamplesOrder(pendingExamplesId);
    runFirstExample();
  }

  action runFirstExample() {
    if pendingExamplesId.size() > 0 then {
      integer exId := pendingExamplesId[0];

       route RunExample(exId);
       on wait ((examples[exId].config.getTimeout()*2.0) + 0.5) and not SpecDone(exId) {
         route org.onspec.internal.Error(exId, "example died unexpectedly");
         route org.onspec.Done();
       }
       on completed SpecDone(exId) {
         pendingExamplesId.remove(0);
         runFirstExample();
       }
    } else {
      endSuite();
    }
  }

  action defineExamplesOrder(sequence<integer> list) returns sequence<integer> {
    if config.shouldRandomize() then {
      RandomGenerator generator := new RandomGenerator;
      generator.init(config.getRandomSeed());
      return Shuffler(generator).shuffle(list);
    } else {
      return list;
    }
  }

  action expandContextString(integer exampleId) returns string {
    return " ".join(getContextDescriptionList(exampleId));
  }

  action getContextDescriptionList(integer exampleId) returns sequence<string> {
    sequence<string> ctxList := contextsToTexts(examples[exampleId].parentContextsId);
    ctxList.append(examples[exampleId].text);
    return ctxList;
  }

  action contextsToTexts(sequence<integer> ctxIds) returns sequence<string> {
    sequence<string> text := [];
    integer ctxId;
    for ctxId in ctxIds {
      text.append(contexts[ctxId].text);
    }
    return text;
  }

  action endSuite() {
    suiteResult.duration := time.getMicroTime() - beginTime;
    route suiteResult;
    die;
  }
}

/** @private */
monitor ExampleResultListener {
  action onload() {
    RunExample ex;
    on all RunExample():ex {
      spawn listenResult(ex.id);
    }
  }

  action listenResult(integer id) {
    org.onspec.Skip s;
    on org.onspec.Skip():s {
      route Skip(id);
    }

    on org.onspec.Success() {
      route Success(id);
    }

    org.onspec.Failure f;
    on all org.onspec.Failure():f {
      route Failure(id, f.reason);
    }

    on org.onspec.Done() {
      enqueue SpecDone(id);
    }

    on SpecDone(id) {
      die;
    }
  }
}


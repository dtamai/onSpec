package com.dtamai.onspec;

/** @private */
event SpecRunner {

  integer _specId;

  //TODO: configuration?
  constant float timeout := 0.2;

  action<string> returns integer addContext;
  action discoverContext(string text) returns integer {
    integer id := _getId();
    __contexts[id] := _ContextDescription(id, text, __currContext.idSequenceToOpen.clone());
    enqueuePendingContextId(id);

    _exampleRootsTags.append(_currTags);
    _currTags := "";

    return id;
  }

  action<string> returns integer addSpec;
  action discoverExample(string text) returns integer {
    integer id := _getId();
    __examples[id] := _SpecDescription(id, text, __currContext.idSequenceToOpen.clone());
    enqueuePendingExampleId(id);

    _currTags := "";

    return id;
  }

  integer _repeatingContextIndex;
  action repeatContextsId(string text) returns integer {
    integer id := __contexts.keys()[_repeatingContextIndex];
    enqueuePendingContextId(id);
    _repeatingContextIndex := _repeatingContextIndex + 1;
    return id;
  }

  integer _repeatingExampleIndex;
  action repeatExamplesId(string text) returns integer {
    integer id := __examples.keys()[_repeatingExampleIndex];
    enqueuePendingExampleId(id);
    _repeatingExampleIndex := _repeatingExampleIndex + 1;
    return id;
  }

  action enqueuePendingContextId(integer contextId) {
    _pendingContextsId.append(contextId);
    _pendingExamplesIdByContext.add(contextId, new sequence<integer>);
  }

  action enqueuePendingExampleId(integer exampleId) {
    _pendingExamplesIdByContext[_cursor].append(exampleId);
  }

  action addTags(string tags) {
    _currTags := tags;
  }

  action run(action<> runnable, integer specId) {
    self._specId := specId;
    _entryPoint := runnable;
    discoverAllExamples(_doSpawn);
  }


  // privates

  action _getCurrentContext() returns sequence<string> {
    return _buildContextForExample(_cursor);
  }

  action _buildContextForExample(integer exampleId) returns sequence<string> {
    sequence<string> contexts := [];
    integer ctxId;
    for ctxId in __examples[exampleId].parentContextsId {
      contexts.append(__contexts[ctxId].text);
    }
    contexts.append(__examples[exampleId].text);
    return contexts;
  }

  action _getId() returns integer{
    return integer.getUnique();
  }

  action _listenProgressionEvents() {
    Skip s;
    on all Skip():s {
      route _Skip(_getCurrentContext());
    }

    on all Success() {
      route _Success();
    }

    Failure f;
    on all Failure():f {
      route _Failure(f.reason, _getCurrentContext());
    }

    on all Done() {
      route _SpecDone(_cursor);
    }
  }

  action discoverAllExamples(action<> nextStep) {
    __currContext := _createRootContext(self._specId);
    prepareDiscovery();
    rewindSpecMonitor();
    openContextsForDiscovery(nextStep);
  }

  action prepareDiscovery() {
    addContext := discoverContext;
    addSpec := discoverExample;
  }

  action openContextsForDiscovery(action<> nextStep) {
    if _pendingContextsId.size() > 0 then {
      _cursor := _pendingContextsId[0];
      _updateCurrentContext(_cursor);

      route Spec(_cursor);
      on completed Spec(_cursor) {
        _pendingContextsId.remove(0);
        openContextsForDiscovery(nextStep);
      }
    } else {
      disableDiscovery();
      nextStep();
      die;
    }
  }

  action rewindSpecMonitor() {
    _repeatingContextIndex := 0;
    _repeatingExampleIndex := 0;
    _entryPoint();
    _cursor := _pendingContextsId[0];
  }

  action _doSpawn() {
    spawn _runEntry();
  }

  action _runEntry() {
    _listenProgressionEvents();
    rewindSpecMonitor();
    _runContext();
  }

  action disableDiscovery() {
    addContext := repeatContextsId;
    addSpec := repeatExamplesId;
  }

  action _runContext() {
    if _pendingContextsId.size() > 0 then {
      _cursor := _pendingContextsId[0];

      route Spec(_cursor);
      on completed Spec(_cursor) {
        _runExample(_cursor);
      }
    } else {
      route _SpecEnd(self._specId);
      die;
    }
  }

  action _runExample(integer contextId) {
    if _pendingExamplesIdByContext[contextId].size() > 0 then {
      integer exampleId := _pendingExamplesIdByContext[contextId][0];
      _cursor := exampleId;

      spawn _reallyRun(exampleId);
      on _SpecDone(exampleId) {
        _pendingExamplesIdByContext[contextId].remove(0);
        _runExample(contextId);
      }
    } else {
      _pendingContextsId.remove(0);
      _runContext();
    }
  }

  action _reallyRun(integer exampleId) {
    rewindSpecMonitor();
    _SpecDescription example := __examples[exampleId];

    integer ctxId;
    for ctxId in example.parentContextsId {
      route Spec(ctxId);
    }

    route Spec(exampleId);
    on wait (timeout) {
      SpecReporter().fail("timeout");
    }
    on _SpecDone(exampleId) {
      die;
    }
  }

  action _createRootContext(integer id) returns _ContextDescription {
    _ContextDescription c := new _ContextDescription;
    c.id := id;
    c.idSequenceToOpen := [];
    return c;
  }

  action _updateCurrentContext(integer id) {
    __currContext := __contexts[id];
    __currContext.idSequenceToOpen.append(id);
  }

  action<> _entryPoint;
  string _currTags;
  integer _cursor;
  sequence<integer> _pendingContextsId;
  sequence<string> _exampleRootsTags;
  dictionary<integer, sequence<integer> > _pendingExamplesIdByContext;

  // Holds all contexts, its is populated in the first run and should be
  // read-only after discovery is done
  dictionary<integer, _ContextDescription> __contexts;

  // Holds all examples, it is populated in the first run and should be
  // read-only after discovery is done
  dictionary<integer, _SpecDescription> __examples;

  _ContextDescription __currContext;
  chunk __deadWeight;
}

package com.dtamai.onspec;

/** @private */
event SpecRunner {

  action<string> returns integer addContext;
  action<string> returns integer addSpec;

  action run(action<> runnable, integer specId) {
    self.specId := specId;
    specsMonitorEntryPoint := runnable;
    discoverAllExamples(doSpawn);
  }


  // privates

  action discoverContext(string text) returns integer {
    integer id := getId();
    storeContext(text, id);

    pendingContextsId.append(id);

    return id;
  }

  action discoverExample(string text) returns integer {
    integer id := getId();
    storeExample(text, id);

    return id;
  }

  action repeatContextsId(string text) returns integer {
    return contextsMap[text];
  }

  action repeatExamplesId(string text) returns integer {
    return examplesMap[text];
  }

  action storeContext(string text, integer id) {
    contexts[id] := _ContextDescription(id, text, surroundingContext.idSequenceToOpen.clone());
    contextsMap[text] := id;
    contextsOrder.append(id);
  }

  action storeExample(string text, integer id) {
    examplesMap[text] := id;
    examplesOrder.append(id);
    examples[id] := _SpecDescription(id, text, surroundingContext.idSequenceToOpen.clone());
  }

  action getId() returns integer{
    return integer.getUnique();
  }

  action discoverAllExamples(action<> nextStep) {
    surroundingContext := createRootContext(self.specId);
    prepareDiscovery();
    rewindSpecMonitor();
    openContextsForDiscovery(nextStep);
  }

  action prepareDiscovery() {
    addContext := discoverContext;
    addSpec := discoverExample;
  }

  action openContextsForDiscovery(action<> nextStep) {
    if pendingContextsId.size() > 0 then {
      cursor := pendingContextsId[0];
      updateCurrentContext(cursor);

      route Spec(cursor);
      on completed Spec(cursor) {
        pendingContextsId.remove(0);
        openContextsForDiscovery(nextStep);
      }
    } else {
      disableDiscovery();
      nextStep();
      die;
    }
  }

  action disableDiscovery() {
    addContext := repeatContextsId;
    addSpec := repeatExamplesId;
  }

  action rewindSpecMonitor() {
    specsMonitorEntryPoint();
    cursor := contextsOrder[0];
  }

  action doSpawn() {
    spawn runEntry();
  }

  action runEntry() {
    listenProgressionEvents();
    runExamples();
  }

  action runExamples() {
    pendingExamplesId := Shuffler(new RandomGenerator).shuffle(examplesOrder);
    runFirstExample();
  }

  action runFirstExample() {
    if pendingExamplesId.size() > 0 then {
      integer exId := pendingExamplesId[0];
      cursor := exId;

      spawn runExample(exId);
      on _SpecDone(exId) {
        pendingExamplesId.remove(0);
        runFirstExample();
      }
    } else {
      route _SpecEnd(self.specId);
      die;
    }
  }

  action runExample(integer exampleId) {
    rewindSpecMonitor();
    _SpecDescription example := examples[exampleId];

    integer ctxId;
    for ctxId in examples[exampleId].parentContextsId {
      route Spec(ctxId);
    }

    route Spec(exampleId);
    on wait (timeout) {
      SpecReporter().fail("timeout");
    }
    on _SpecDone(exampleId) {
      die;
    }
  }

  action getCurrentContextDescriptionList() returns sequence<string> {
    sequence<string> ctxList := [];
    integer ctxId;
    for ctxId in examples[cursor].parentContextsId {
      ctxList.append(contexts[ctxId].text);
    }
    ctxList.append(examples[cursor].text);
    return ctxList;
  }

  action createRootContext(integer id) returns _ContextDescription {
    _ContextDescription c := new _ContextDescription;
    c.id := id;
    c.idSequenceToOpen := [];
    return c;
  }

  action updateCurrentContext(integer id) {
    surroundingContext := contexts[id];
    surroundingContext.idSequenceToOpen.append(id);
  }

  action listenProgressionEvents() {
    Skip s;
    on all Skip():s {
      route _Skip(getCurrentContextDescriptionList());
    }

    on all Success() {
      route _Success();
    }

    Failure f;
    on all Failure():f {
      route _Failure(f.reason, getCurrentContextDescriptionList());
    }

    on all Done() {
      route _SpecDone(cursor);
    }
  }

  // Identifies the client monitor
  integer specId;

  //TODO: configuration?
  constant float timeout := 0.2;

  // Action in the client monitor that contains the specs
  action<> specsMonitorEntryPoint;

  dictionary<string /*text*/, integer /*id*/> contextsMap;
  sequence<integer /*id*/> contextsOrder;
  dictionary<string /*text*/, integer /*id*/> examplesMap;
  sequence<integer /*id*/> examplesOrder;

  // Current context/example. While in discovery, its value is the current
  // context; while running examples, its value is the current example
  // This variable is not relevant for the monitor that is spawned to run
  // examples
  integer cursor;

  // Used while discovering examples, holds the _ContextDescription of the
  // most internal context that is outside the current context.
  _ContextDescription surroundingContext;

  // Used while discovering examples, holds the ids of contexts still closed
  sequence<integer> pendingContextsId;
  // Used while running examples, is a modifiable copy of the examples list
  sequence<integer> pendingExamplesId;

  // Holds all contexts, its is populated in the first run and must be
  // read-only after discovery is done
  dictionary<integer /*id*/, _ContextDescription> contexts;

  // Holds all examples, it is populated in the first run and must be
  // read-only after discovery is done
  dictionary<integer /*id*/, _SpecDescription> examples;

  chunk __deadWeight;
}

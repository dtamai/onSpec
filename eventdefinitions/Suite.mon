package com.dtamai.onspec;

event Suite {
  wildcard integer _id;
  SpecRunner _runner;
  action describe(string text) returns integer {
    return _runner.addContext(text);
  }

  action it(string text) returns integer {
    return _runner.addSpec(text);
  }

  action T(string tags) {
    _runner.addTags(tags);
  }

  action init(action<> runnable) {
    self._id := integer.getUnique();
    on all NewSuite() -> _SpecDiscover() {
      spawn _handleNewSuite(runnable);
    }
  }

  action _handleNewSuite(action<> runnable) {
    route _SpecPresent(self._id);
    on _SpecBegin(_id) {
      _runner.run(runnable, self._id);
    }
  }

  action pass() {
    route Success();
    done();
  }

  action skip() {
    route Skip();
    //TODO: report which test was skipped
    done();
  }

  action fail(string reason) {
    route _Failure(reason, _runner.getCurrentContext());
    //TODO: report which test failed
    done();
  }

  action done() {
    route _SpecDone(_runner.getCurrentSpecId());
  }

  action assertTrue(boolean test) {
    //TODO: report counter
    _AssertionBoolean r := new _AssertionBoolean;
    r.actual := test;
    r.eq(true);
  }

  action assertBoolean(boolean actual) returns _AssertionBoolean {
    //TODO: report counter
    _AssertionBoolean r := new _AssertionBoolean;
    r.actual := actual;
    return r;
  }

  action assertInteger(integer actual) returns _AssertionInteger {
    //TODO: report counter
    _AssertionInteger r := new _AssertionInteger;
    r.actual := actual;
    return r;
  }

  action assertFloat(float actual) returns _AssertionFloatRelative {
    //TODO: report counter
    _AssertionFloatRelative r := new _AssertionFloatRelative;
    r.actual := actual;
    return r;
  }

  action assertString(string actual) returns _AssertionString {
    //TODO: report counter
    _AssertionString r := new _AssertionString;
    r.actual := actual;
    return r;
  }
}

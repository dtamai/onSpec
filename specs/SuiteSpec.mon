package org.onspec;

using org.onspec.expectation.IntegerValue;

event SuiteSpec_Passed {}

monitor SuiteSpec {
  Suite _;

  action onload() {
    _.init(run);
  }

  action run() {
    on Spec(_.describe("Suite")) {
      on Spec(_.it("generates an unique id after initialization")) {
        Suite suite1 := dummySuite();
        Suite suite2 := dummySuite();

        IntegerValue(suite1.getId()).mustNotBe(suite2.getId());
        _.done();
      }

      on Spec(_.it("answers to SpecDiscover")) {
        spawn testAnswerSpecDiscover to context("isolated");
        on SuiteSpec_Passed() {
          _.pass();
        }
      }
    }
  }

  action testAnswerSpecDiscover() {
    on _SpecPresent() { enqueue SuiteSpec_Passed(); }

    Suite suite := dummySuite();
    route NewSuite();
    route _SpecDiscover();
  }

  action dummySuite() returns Suite {
    Suite s := new Suite;
    s.init(dummy);
    return s;
  }

  action dummy() {}
}

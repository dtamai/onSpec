package org.onspec;

event SpecReporterSpec_Passed {}
event SpecReporterSpec_NotDone {}
monitor SpecReporterSpec {

  Suite _;

  action onload() {
    _.init(run);
  }

  action run() {
    on Spec(_.describe("SpecReporter")) {
      on Spec(_.it("terminates tests")) {
        spawn testTerminate to context("isolated");
        on SpecReporterSpec_Passed() {
          _.pass();
        }
      }

      on Spec(_.it("terminates after success")) {
        spawn testSuccessThenTerminate to context("isolated");
        on SpecReporterSpec_Passed() {
          _.pass();
        }
      }

      on Spec(_.it("terminates after failure (async)")) {
        spawn testFailThenTerminate to context("isolated");
        on SpecReporterSpec_Passed() {
          _.pass();
        }
      }

      on Spec(_.it("does not terminate after failure (sync)")) {
        spawn testFailAndContinue to context("isolated");
        on SpecReporterSpec_Passed() {
          _.pass();
        }
      }

      on Spec(_.it("terminates after skip")) {
        spawn testSkipThenTerminate to context("isolated");
        on SpecReporterSpec_Passed() {
          _.pass();
        }
      }
    }
  }

  action testTerminate() {
    on Done() { enqueue SpecReporterSpec_Passed(); }

    SpecReporter().done();
  }

  action testSuccessThenTerminate() {
    on Success() -> Done() { enqueue SpecReporterSpec_Passed(); }

    SpecReporter().passAndTerminate();
  }

  action testFailThenTerminate() {
    string reason := "SpecReportSpec failed";
    Failure f;
    on Failure():f -> Done() {
      if f.reason = reason then {
        enqueue SpecReporterSpec_Passed();
      }
    }

    SpecReporter().failAndTerminate(reason);
  }

  action testFailAndContinue() {
    string reason := "SpecReporterSpec failed";
    Failure f;
    on (Failure():f -> SpecReporterSpec_NotDone()) and not Done() {
      if f.reason = reason then {
        enqueue SpecReporterSpec_Passed();
      }
    }

    SpecReporter().fail(reason);
    route SpecReporterSpec_NotDone();
  }

  action testSkipThenTerminate() {
    on Skip() -> Done() { enqueue SpecReporterSpec_Passed(); }

    SpecReporter().skipAndTerminate();
  }
}
